dnl configure.ac
dnl
dnl xfce4-mpc-plugin - mpd client plugin for the XFce4 panel
dnl

m4_define([mpc_version_major], [0])
m4_define([mpc_version_minor], [4])
m4_define([mpc_version_micro], [4])
m4_define([mpc_version_tag], []) # Leave empty for releases
m4_define([mpc_version_build], [@REVISION@])
m4_define([mpc_version], [mpc_version_major().mpc_version_minor().mpc_version_micro()ifelse(mpc_version_tag(), [], [], [mpc_version_tag()-mpc_version_build()])])

AC_INIT([xfce4-mpc-plugin], [mpc_version],
	[landry@xfce.org])

MPC_VERSION=mpc_version()
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar no-dist-gzip])
AM_CONFIG_HEADER([config.h])
dnl AC_CONFIG_HEADERS([config.h])

AM_MAINTAINER_MODE

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
IT_PROG_INTLTOOL([0.35.0])

dnl Initialize libtool
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl Check for standard header files
AC_HEADER_STDC

dnl configure the panel plugin
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.8.0])
XDT_CHECK_PACKAGE([EXO], [exo-1], [0.5.0])

dnl check for libmpd presence
AC_ARG_WITH(libmpd,
    AC_HELP_STRING([--with-libmpd=path], [path to libmpd base directory]),
        LIBMPD_PATH="$withval", LIBMPD_PATH="")

use_libmpd=false
AM_CONDITIONAL([HAVE_LIBMPD], [test "$use_libmpd" = true])
AC_DEFINE([HAVE_LIBMPD], [0], [no libmpd found])

AC_MSG_CHECKING([whether --with-libmpd was used])
if test x"$LIBMPD_PATH" != x"" ; then
   if test x"$LIBMPD_PATH" = x"no" ; then
      AC_MSG_RESULT([yes, disabled])
      echo "* Build with libmpd: no, falling back to our implementation"
   else
      AC_MSG_RESULT([yes, using $LIBMPD_PATH])
      LIBMPD_CFLAGS="-I${LIBMPD_PATH}/include/"
      LIBMPD_LIBS="-L${LIBMPD_PATH}/lib/ -lmpd"
      AC_DEFINE([HAVE_LIBMPD], [1], [libmpd found])
      use_libmpd=true
      AC_SUBST([LIBMPD_CFLAGS])
      AC_SUBST([LIBMPD_LIBS])
   fi
else
   AC_MSG_RESULT([no, falling back to pkg-config])
   XDT_CHECK_OPTIONAL_PACKAGE([LIBMPD], [libmpd], [0.12], [libmpd], [libmpd])
   if test x"$LIBMPD_FOUND" = x"yes"; then
      AC_DEFINE([HAVE_LIBMPD], [1], [libmpd found])
      use_libmpd=true
      echo "* Build with libmpd: yes"
   else
      echo "* Build with libmpd: no, falling back to our implementation"
   fi
fi
AM_CONDITIONAL(USE_LIBMPD, [test "$use_libmpd" = true])
dnl Translations
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()
AC_OUTPUT([
Makefile
panel-plugin/Makefile
po/Makefile.in
])

